package org.verapdf.model.coslayer;

import org.verapdf.model.baselayer.Object;
import org.verapdf.model.pdlayer.PDMetadata;
import org.verapdf.model.pdlayer.PDDocument;
import java.util.List;

% Parent type for all basic PDF objects
type CosObject extends Object {

}

% Low-level PDF Document object 
type CosDocument extends CosObject {
	% Number of indirect objects in the document
	property nrIndirects: Integer;
	% Size of the byte sequence representing the document 
	property size: Integer;
	% The document catalog dictionary shall not contain a key with the name OCProperties.
	property isOptionalContentPresent: Boolean;
	% true if the end of PDF file complies PDF/A requirements (no data after the last end-of-file marker except a single optional end-of-line marker)
	property eofCompliesPDFA : Boolean;
	% check matches of Info dictionary and XMP metadata if first present
	property doesInfoMatchXMP: Boolean
	% link to the document cross reference table
	link xref: CosXRef;
	% trailer dictionary
	link trailer: CosTrailer;
	% all indirect objects referred from the xref table
	link indirectObjects: CosIndirect+;
	% true if the first line of the document satisfy to PDF/A standard
	property pdfHeaderCompliesPDFA: Boolean;
	% true if the second line of the document is a comment with at least 4 symbols in the code range 128-255 as required by PDF/A standard
	property binaryHeaderComplyPDFA: Boolean;
	% link to the high-level PDF Document structure
	link document: PDDocument;
}

% PDF Indirect object
type CosIndirect extends CosObject {
	% the direct contents of the indirect object
	link directObject: CosObject;
	% true if the words obj and endobj are surrounded by the correct spacings according to PDF/A standard
	property spacingCompliesPDFA: Boolean;
}

% PDF null object	
type CosNull extends CosObject {
	
}

% PDF boolean object
type CosBool extends CosObject {
	% value of this object
	property value: Boolean;
}

% Generic Number type
type CosNumber extends CosObject {
	% the string representing this object
	property stringValue: String;
	% truncated integer value
	property intValue: Integer;
	% original decimal value
	property realValue: Decimal;
}

% PDF Real type	
type CosReal extends CosNumber {

}

% PDF Integer type
type CosInteger extends CosNumber {
	
}

% PDF Name type	
type CosName extends CosObject {
	% Unicode string representation of the Name object after applying escape mechanism and converting to Unicode using Utf8 encoding
	property value: String;
	% Original length of the name before applying any escape mechanisms and encodings
	property origLength: Integer;
}
	
% PDF String type
type CosString extends CosObject {
	% Unicode string value stored in the PDF object
	property value: String;
	% if the string is stored in Hex format
	property isHex: Boolean;
	% true if the string contains only hex symbols (belong to 0-9, a-f, A-F range)
	property isHexSymbols: Boolean;
	% original string value of the string before applying Hex decoding and any encodings (but after ignoring all white spaces)
	property origValue: String;	
}

% PDF Dict type
type CosDict extends CosObject {
	% number of key/value pairs in the dictionary
	property size: Integer;
	% all keys of the dictionary
	link keys: CosName*;
	% all values of the dictionary
	link values: CosObject*;
	% XMP metadata if it is present
	link metadata: PDMetadata?;
}
	
% PDF Stream type
type CosStream extends CosDict {
	% length of the stream
	property Length: Integer;
	% concatenated (white space separated) names of all filters
	property filters: String;
	% represents value of F key or null if stream does not contain this key
	property F: String;
	% represents value of FFilter key or null if stream does not contain this key
	property FFilter: String;
	% represents value of FDecodeParms key or null if stream does not contain this key
	property FDecodeParms: String;
	% true if the spacing around stream / endstream complies with the PDF/A requirements
	property spacingCompliesPDFA: Boolean;
	% true if the value of Length key matches the actual length of the stream
	property isLengthCorrect: Boolean;
}

% File descriptor dictionary		
type CosFileDescriptor extends CosDict {
	
}
	
% Trailer of the document	
type CosTrailer extends CosDict {
	% document id
	property documentId: String;
	% id of first page trailer, if document is linearized
	property firstPageID: String;
	% id of the last trailer
	property lastID: String;
	% true if the document is linearized
	property isLinearized: Boolean;
	% true if the document is encrypted
	property isEncrypted: Boolean;
	% catalog dictionary of pdf document
	link Catalog: CosIndirect;
}

% PDF Array type	
type CosArray extends CosObject {
	% number of elements in the array
	property size: Integer;
	% all objects contained in the array
	link elements: CosObject*;
}
	
% Bounding box array
type CosBBox extends CosArray {
	property top: Decimal;
	property bottom: Decimal;
	property left: Decimal;
	property right: Decimal;
}

% Cross reference table of the document
type CosXRef extends CosObject {
	% true if header of cross reference table comply PDF/A standard
	property xrefHeaderSpacingsComplyPDFA: Boolean;
	% true if xref keyword and cross reference subsection separated by single EOL
	property xrefEOLMarkersComplyPDFA: Boolean;
}